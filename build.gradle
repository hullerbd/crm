buildscript {
    ext {
        springBootVersion = '2.0.2.RELEASE'
    }
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("com.palantir.gradle.docker:gradle-docker:0.19.2")
        classpath("com.moowork.gradle:gradle-node-plugin:1.2.0")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.moowork.node'
apply plugin: 'com.palantir.docker'


//========Docker - Start
// This is used as the docker image prefix (org)
group = 'james-busyqa'
bootJar {
    baseName = 'crm'
    version = '0.1.0'
}

docker {
    dependsOn build
    name "${project.group}/${bootJar.baseName}"
    files bootJar.archivePath
    buildArgs(['JAR_FILE': "${bootJar.archiveName}"])
}
//========Docker - End

repositories {
    mavenCentral()
}


dependencies {
    //aop
    compile('org.springframework.boot:spring-boot-starter-aop')

    //web
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-web')

    //thymeleaf
    compile('org.springframework.boot:spring-boot-starter-thymeleaf')

    //JPA

    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('mysql:mysql-connector-java')
    //    compile('org.postgresql:postgresql')

    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile group: 'com.github.springtestdbunit', name: 'spring-test-dbunit', version: '1.3.0'

}

// ========
node {
    version = "8.11.2"
    npmVersion = "6.1.0"
    download = true
    nodeModulesDir = file("frontend")
}
//======== for build dev
task buildAllFast(type: NpmTask, dependsOn: 'npmInstall') {
    group = 'application'
    description = "Build and watches the client side assets for rebuilding"
    args = ['run', 'buildDevFast']
    nodeSetup.onlyIf {
        !nodeSetup.nodeDir.exists()
    }

    npmSetup.onlyIf {
        !npmSetup.npmDir.exists()
    }
//skip update. remove it if needed
    npmInstall.onlyIf {
        !npmSetup.npmDir.exists()
    }
}

//comm this one, for only backend
//bootRun.dependsOn(buildAllFast)